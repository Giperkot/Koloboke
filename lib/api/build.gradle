evaluationDependsOn("$parent.path")

description = 'API'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // just for Javadoc links
    provided 'com.koloboke:koloboke-compile:0.5'
}
poms*.whenConfigured { pom ->
    pom.dependencies.removeAll { dep -> dep.artifactId == 'koloboke-compile' }
}

configure(javadoc) {
    title "Koloboke Collections $apiVersion API"
    configure((StandardJavadocDocletOptions) getOptions()) {
        def javaDocJava = project.hasProperty('javadocJava') ? javadocJava : libTargetJava
        links("https://javadoc.io/doc/com.google.code.findbugs/annotations/$findbugs_version/",
                "http://leventov.github.io/Koloboke/compile/0.5/",
                "http://docs.oracle.com/javase/$javaDocJava/docs/api/")
        overview "$buildDir/generated-src/jpsg/main/java/overview.html"
        addStringOption('sourcepath', project.hasProperty('jdkSrc') ? jdkSrc :
                "$System.env.JAVA_HOME/src")
        if (libTargetJava == '8')
            // to suppress warnings in JDK sources
            addStringOption('tag', 'implSpec')
        addStringOption('noqualifier', 'java.lang:java.util')
        use = true // generate Use pages
    }
    executable = project.hasProperty('javadocExecutable') ? javadocExecutable : null
}