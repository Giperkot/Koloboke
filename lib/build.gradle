buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath project(':jpsg:gradle-plugin')
        classpath project('template-processors')
        classpath project('impl-generator')
	    classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
	}
}

if (libTargetJava != '6' && libTargetJava != '8')
    throw new GradleException('libTargetJava should be either 6 or 8')

def apiProject = project('api')
def implProject = project('impl')

configure([apiProject, implProject, project('testing')]) {
    applyJpsg()

    def jpsgTasks = [generateJavaSpecializations]
    def generateTestSpecializations = tasks.findByName('generateTestJavaSpecializations')
    if (generateTestSpecializations != null)
        jpsgTasks << generateTestSpecializations
    configure(jpsgTasks) {
        addProcessor 'net.openhft.jpsg.FunctionProcessor'
        addProcessor 'net.openhft.jpsg.Jdk8FunctionReplacer'
        with "jdk=JDK$libTargetJava"
    }
}

configure([apiProject, implProject]) {
    apply plugin: 'jacoco'

    setSourceCompatibility(project, "1.$libTargetJava")

    dependencies {
        provided 'com.google.auto.value:auto-value:1.0-rc1'
    }

    idea.module {
        excludeDirs.remove(file("$buildDir/classes"))
        // AutoValue implementations are located there
        sourceDirs += sourceSets*.output.classesDir
    }
}

configure([apiProject, implProject]) {
    configure(compileJava) {
        // To suppress warnings about usages of sun.misc.Unsafe API
        options.compilerArgs << '-XDignore.symbol.file'
        options.compilerArgs << '-Xlint:-deprecation' << '-Xlint:-overloads'
        options.fork = true
        def javac = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'
        options.forkOptions.executable = javac
    }

    test.testLogging.exceptionFormat = 'full'

    configurePublishing(project, !project.is(implProject))
    archivesBaseName  = "koloboke-$project.name-jdk${libTargetJava == '6' ? '6-7' : '8'}"
    version = '0.7-SNAPSHOT'
    ext.apiVersion = '0.6'

    poms*.whenConfigured { pom ->
        pom.project {
            name = "Koloboke Collections ${project.description}"
            description = 'Carefully designed and efficient extension ' +
                    'of the Java Collections Framework with primitive specializations and more, ' +
                    "built for Java ${libTargetJava == '6' ? '6 or 7' : '8'} " +
                    "(${project.description})"
        }
    }
}